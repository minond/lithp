;;;; Standard Library

;;; Building Blocks

;; We've already come up with a number of cool functions I've been using in the
;; examples. One of these is our fun function that allows us to declare
;; functions in a neater way. We should definitely include this in our standard
;; library.
(def {fun}
  (\ {args body}
    {def (head args)
      (\ (tail args) body)}))

;; We also had our unpack and pack functions. These too are going to be
;; essential for users. We should include these along with their curry and
;; uncurry aliases.
(fun {unpack f xs} {eval (join (list f) xs)})
(fun {pack f & xs} {f xs})

(def {uncurry} pack)
(def {curry} unpack)

;; Say we want to do several things in order. One way we can do this is to put
;; each thing to do as an argument to some function. We know that arguments are
;; evaluated in order from left to right, which is essentially sequencing
;; events. For functions such as print and load we don't care much about what
;; it evaluates to, but do care about the order in which it happens. Therefore
;; we can create a do function which evaluates a number of expressions in order
;; and returns the last one. This relies on the last function, which returns
;; the final element of a list. We'll define this later.
(fun {do & l}
  {if (== l nil)
    {nil}
    {last l}})

;; Sometimes we want to save results to local variables using the = operator.
;; When we're inside a function this will implicitly only save results locally,
;; but sometimes we want to open up an even more local scope. For this we can
;; create a function let which creates an empty function for code to take place
;; in, and evaluates it.
(fun {let b} {
  ((\ {_} b) ())})

;;; Miscellaneous Functions

;; The flip function takes a function f and two arguments a and b. It then
;; applies f to a and b in the reversed order. This might be useful when we
;; want a function to be partially evaluated. If we want to partially evaluate
;; a function by only passing it in it's second argument we can use flip to
;; give us a new function that takes the first two arguments in reversed order.
;; This means if you want to apply the second argument of a function you can
;; just apply the first argument to the flip of this function.
(fun {flip f a b} {f b a})

;; I couldn't think of a use for the ghost function, but it seemed interesting.
;; It simply takes in any number of arguments and evaluates them as if they
;; were the expression itself. So it just sits at the front of an expression
;; like a ghost, not interacting with or changing the behaviour of the program
;; at all.  If you can think of a use for it I'd love to hear.
(fun {ghost & xs} {eval xs})

;; The comp function is used to compose two functions. It takes as input f, g,
;; and an argument to g. It then applies this argument to g and applies the
;; result again to f. This can be used to compose two functions together into a
;; new function that applies both of them in series. Like before, we can use
;; this in combination with partial evaluation to build up complex functions
;; from simpler ones. For example we can compose two functions. One negates a
;; number and another unpacks a list of numbers for multiplying together with
;; *.
(fun {comp f g x} {f (g x)})

;;; List Functions

;; The head function is used to get the first element of a list, but what it
;; returns is still wrapped in the list. If we want to actually get the element
;; out of this list we need to extract it somehow. Single element lists
;; evaluate to just that element, so we can use the eval function to do this
;; extraction.  We can also define a couple of helper function sfor aid extrating
;; the first, second, and third elements of a list.
(fun {fst l} {eval (head l)})
(fun {snd l} {eval (head (tail l))})
(fun {trd l} {eval (head (tail (tail l)))})

;; To find the length of a list we can resursive over it adding 1 to the length
;; of the tail. To find the nth element of a list we can perform the tail
;; operation and count down until we reach 0. To get the last element of a list
;; we can just access the element at the length minus one.
(fun {len l}
  {if (== l nil)
    {0}
    {+ 1 (len (tail l))}})

(fun {nth n l}
  {if (== n 0)
    {fst l}
    {nth (- n 1) (tail l)}})

(fun {last l}
  {nth (- (len l) 1) l})

;; There are lots of other useful functions that follow this same patter. We
;; can define functions for taking and dropping the first so many elements of a
;; list, or functions for checking if a value is an element of a list.
(fun {take n l}
  {if (== n 0)
    {nil}
    {join (head l) (take (- n 1) (tail l))}})

(fun {drop n l}
  {if (== n 0)
    {l}
    {drop (- n 1) (tail l)}})

(fun {split n l}
  {list (take n l) (drop n l)})

(fun {elem x l}
  {if (== l nil)
    {false}
    {if (== x (fst l))
      {true}
      {elem x (tail l)}}})

;; These functions all follow similar patterns. It would be great if there was
;; some way to extract this pattern so we don't have to type it out every time.
;; For eample we may want a way we can perform some function on every element
;; of a list. This is a function we can define called map. It takes as input
;; some function, and some list. For each item in the list it applies f to that
;; item and appends it back onto the front of the list. It then applies map to
;; the tail of the list.
(fun {map f l}
  {if (== l nil)
    {nil}
    {join (list (f (fst l))) (map f (tail l))}})

;; An adaptation of this idea is a filter function which, takes in some
;; functional condition, and only includes items of a list which match that
;; condition.
(fun {filter f l}
  {if (== l nil)
    {nil}
    {join (if (f (fst l))
      {head l}
      {nil}) (filter f (tail l))}})

;; Some loops don't exactly act on a list, but accumulate some total or
;; condense the list into a single value. These are loops such as sums and
;; products. These can be expressed quite similarly to the len function we've
;; already defined. These are called folds and they work like this. Supplied
;; with a function f, a base value z and a list l they merge element in the
;; list with the total, starting with the base value.
(fun {foldl f z l}
  {if (== l nil)
    {z}
    {foldl f (f z (fst l)) (tail l)}})

(fun {sum l}
  {foldl + 0 l})

(fun {product l}
  {foldl * 1 l})

; vim:ft=clojure
